// Main rules
source = { SOI ~ (node_def | edge_def | vector_def | query_def)* ~ EOI }

// Query definitions
query_def    = { "QUERY" ~ identifier ~ query_params ~ "=>" ~ query_body ~ return_stmt }
query_params = { "(" ~ (param_def ~ ("," ~ param_def)*)? ~ ")" }
param_def    = { identifier ~ ":" ~ type_name }
query_body   = { (get_stmt | AddN | AddV | BatchAddV | AddE | drop | search_vector)* }

// Assignments and traversals
get_stmt            = { identifier ~ "<-" ~ evaluates_to_anything }
traversal           = { (start_vertex | start_edge | start_vector) ~ step* ~ last_step? }
id_traversal        = { identifier ~ ((step+ ~ last_step?) | last_step) }
anonymous_traversal = { "_" ~ ((step+ ~ last_step?) | last_step) }
step                = { "::" ~ (graph_step | where_step | closure_step | object_step | exclude_field | count | ID | range_step | AddE) }
last_step           = { "::" ~ (bool_operations | update) }

// Evaluation rules for different types
evaluates_to_anything = {
    AddN
  | AddV
  | BatchAddV
  | AddE
  | exists
  | none
  | traversal
  | id_traversal
  | string_literal
  | float
  | integer
  | boolean
  | and
  | or
  | identifier
}

evaluates_to_bool = {
    exists
  | boolean
  | and
  | or
  | identifier
  | traversal
  | id_traversal
}

evaluates_to_number = {
    float
  | integer
  | identifier
  | traversal
  | id_traversal
}

// Return statement
return_stmt = { "RETURN" ~ evaluates_to_anything ~ ("," ~ evaluates_to_anything)* }

// Vertex-related steps
start_vertex = { "N" ~ ("<" ~ type_args ~ ">")? ~ ("(" ~ id_args ~ ")")? }
graph_step   = {
    "OutE" ~ ("<" ~ type_args ~ ">")?
  | "InE" ~ ("<" ~ type_args ~ ">")?
  | "BothE" ~ ("<" ~ type_args ~ ">")?
  | "OutN" ~ ("<" ~ type_args ~ ">")?
  | "InN" ~ ("<" ~ type_args ~ ">")?
  | "BothN" ~ ("<" ~ type_args ~ ">")?
  | "Out" ~ ("<" ~ type_args ~ ">")?
  | "In" ~ ("<" ~ type_args ~ ">")?
  | "Both" ~ ("<" ~ type_args ~ ">")?
}

// Edge-related steps
start_edge = { "E" ~ ("<" ~ type_args ~ ">")? ~ ("(" ~ id_args ~ ")")? }

// Vector-related steps
start_vector = { "V" ~ ("<" ~ type_args ~ ">")? ~ ("(" ~ id_args ~ ")")? }

// Where and Exists steps
where_step = { "WHERE" ~ "(" ~ (evaluates_to_bool | anonymous_traversal) ~ ")" }
exists     = { "EXISTS" ~ "(" ~ (traversal | id_traversal | anonymous_traversal) ~ ")" }

// Search
search_vector = { "SearchV" ~ "<" ~ identifier_upper ~ ">" ~ "(" ~ vector_data ~ "," ~ integer ~ ")" }
BatchAddV = { "BatchAddV" ~ "<" ~ identifier_upper ~ ">" ~ "(" ~ identifier ~ ")" }

// Arguments for various steps
type_args = { identifier_upper ~ ("," ~ identifier_upper)* }
id_arg    = { (identifier | string_literal) }
id_args   = { (id_arg) ~ ("," ~ id_arg)* }

// Range step
range_step = { "RANGE" ~ "(" ~ (evaluates_to_number) ~ "," ~ (evaluates_to_number) ~ ")" }

// Boolean operations
and             = { "AND" ~ "(" ~ (evaluates_to_bool | anonymous_traversal) ~ ("," ~ (evaluates_to_bool | anonymous_traversal))* ~ ")" }
or              = { "OR" ~ "(" ~ (evaluates_to_bool | anonymous_traversal) ~ ("," ~ (evaluates_to_bool | anonymous_traversal))* ~ ")" }
bool_operations = { GT | GTE | LT | LTE | EQ | NEQ }
GT              = { "GT" ~ "(" ~ (evaluates_to_number | anonymous_traversal) ~ ")" }
GTE             = { "GTE" ~ "(" ~ (evaluates_to_number | anonymous_traversal) ~ ")" }
LT              = { "LT" ~ "(" ~ (evaluates_to_number | anonymous_traversal) ~ ")" }
LTE             = { "LTE" ~ "(" ~ (evaluates_to_number | anonymous_traversal) ~ ")" }
EQ              = { "EQ" ~ "(" ~ (evaluates_to_anything | anonymous_traversal) ~ ")" }
NEQ             = { "NEQ" ~ "(" ~ (evaluates_to_anything | anonymous_traversal) ~ ")" }

count        = { "COUNT" }
none         = { "NONE" }
ID           = { "ID" }
update_field = { identifier ~ ":" ~ (evaluates_to_anything | anonymous_traversal) }
update       = { "UPDATE" ~ "(" ~ "{" ~ update_field ~ ("," ~ update_field)* ~ "}" ~ ")" }

object_step   = { "{" ~ mapping_field ~ ("," ~ mapping_field)* ~ ("," ~ spread_object)? ~ "}" }
exclude_field = { "!" ~ "{" ~ identifier ~ ("," ~ identifier)* ~ ("," ~ spread_object)? ~ "}" }
closure_step  = { "|" ~ identifier ~ "|" ~ object_step }
spread_object = { ".." }

mapping_field = { (identifier ~ (":" ~ (anonymous_traversal | evaluates_to_anything | object_step))) | identifier }

// Schema definitions
vector_def = { "V::" ~ identifier_upper }
node_def   = { "N::" ~ identifier_upper ~ node_body }
edge_def   = { "E::" ~ identifier_upper ~ edge_body }

node_body  = { "{" ~ field_defs ~ "}" }
edge_body  = { "{" ~ "From:" ~ identifier_upper ~ "," ~ "To:" ~ identifier_upper ~ "," ~ properties ~ "}" }
field_def  = { (identifier ~ ":" ~ type_name) }
field_defs = { field_def ~ ("," ~ field_def)* }
properties = { "Properties" ~ ":" ~ "{" ~ field_defs? ~ "}" }

// Values and literals
array            = @{ "[" ~ type_name ~ "]" }
string_literal   = ${ "\"" ~ inner_string ~ "\"" }
inner_string     = @{ (!("\"" | "\\") ~ ANY)* }
boolean          =  { "true" | "false" }
type_name        = @{ "String" | "Integer" | "Float" | "Boolean" | array | identifier_upper }
identifier       = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
identifier_upper = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHANUMERIC | "_")* }
integer          = @{ ASCII_DIGIT+ }
float            = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

// Whitespace and comments
WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }

// Add edged and vertices
create_field  = { "{" ~ new_field ~ ("," ~ new_field)* ~ "}" }
new_field     = { identifier ~ ":" ~ (anonymous_traversal | evaluates_to_anything | create_field) }
AddN          = { "AddN" ~ ("<" ~ identifier_upper ~ ">")? ~ ("(" ~ create_field? ~ ")")? }
AddE          = { "AddE" ~ ("<" ~ identifier_upper ~ ">")? ~ ("(" ~ create_field? ~ ")")? ~ to_from }
AddV          = { "AddV" ~ ("<" ~ identifier_upper ~ ">")? ~ ("(" ~ vector_data? ~ ")")? }
vector_data = { identifier | vec_literal }
vec_literal   = { "[" ~ (float) ~ ("," ~ (float))* ~ "]" }
to_from       = { to ~ from? | from ~ to? }
to            = { "::" ~ "To" ~ "(" ~ id_arg ~ ")" }
from          = { "::" ~ "From" ~ "(" ~ id_arg ~ ")" }

drop = { "DROP" ~ (traversal | id_traversal | identifier)? }
